// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonSlurper

ext.kotlin_version = '1.3.21'

/**
 * Versioning by package.json
 * Inspired by https://medium.com/@andr3wjack/versioning-react-native-apps-407469707661
 */
ext.versioning = [
    getNpmVersion: { ->
        def inputFile = new File("../package.json")
        def packageJson = new JsonSlurper().parseText(inputFile.text)
        return packageJson["version"]
    },
    getNpmVersionArray: { -> // major [0], minor [1], patch [2]
        def (major, minor, patch) = versioning.getNpmVersion().tokenize('.')
        return [Integer.parseInt(major), Integer.parseInt(minor), Integer.parseInt(patch)] as int[]
    }
]

subprojects {
    ext {
        def npmVersion = versioning.getNpmVersionArray()
        versionMajor = npmVersion[0]
        versionMinor = npmVersion[1]
        versionPatch = npmVersion[2]
    }
}

buildscript {
    ext {
        //buildToolsVersion = "26.1.1"
        minSdkVersion = 21
        compileSdkVersion = 27
        targetSdkVersion = 27
        supportLibVersion = "27.1.0"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath 'com.google.gms:google-services:4.0.2'
        classpath 'com.google.android.gms:strict-version-matcher-plugin:1.0.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        configurations.all {
            resolutionStrategy {
                // This is all apparently required for react-native-camera to build... no clue why
                // I suppose this has the potential to break things, so keep an eye out
                force 'com.google.android.gms:play-services-base:15.0.1'
                force 'com.google.android.gms:play-services-basement:15.0.1'
                force 'com.google.android.gms:play-services-tasks:15.0.1'
                force 'com.android.support:support-core-utils:27.1.0'
                force 'com.android.support:support-core-ui:27.1.0'
                force 'com.android.support:support-core-ui:27.1.0'
                force 'com.android.support:support-fragment:27.1.0'
                force 'com.android.support:support-compat:27.1.0'
                force 'com.android.support:support-annotations:27.1.0'
                force 'android.arch.lifecycle:common:1.1.0'
                force 'android.arch.core:common:1.1.0'
                // Not sure what needs this
                force "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
            }
        }

        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
        maven { url "https://jitpack.io" }
        maven { url "https://maven.google.com" }
        google()
        mavenLocal()
        jcenter()
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
    distributionUrl = distributionUrl.replace("bin", "all")
}
